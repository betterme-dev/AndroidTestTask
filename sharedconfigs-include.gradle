apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

/**
 * To avoid code duplication all common android modules related setup is done here.
 * When using it you still have to specify stuff like
 *
 * apply plugin: 'com.android.application'
 *
 * or
 *
 * apply plugin: 'com.android.library
 *
 * and all the dependencies via
 *
 * dependencies { ... }*
 * -------------------------------
 *
 * */

ext.isCiServer = System.getenv().containsKey("CI_BUILD")

android {
    defaultConfig {
        buildToolsVersion buildToolsVer
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        compileSdkVersion compileSdkVer
        versionName rootProject.extensions.getByName("ext")["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"
                ]
                includeCompileClasspath = true
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        debug {
            debuggable true
        }
        beta {
            debuggable true
        }
        release {
            debuggable false
        }
    }

    // Forcing proper java version across all the builds
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    if (gradle.startParameter.taskNames
            .collect { item -> item.contains("UnitTestKotlin") }
            .contains(true)) {
        kotlinOptions.jvmTarget = jvmTargetVer
    }

    compileOptions {
        kotlinOptions.jvmTarget = jvmTargetVer
    }

    // Enabling kotlin view binding in adapters
    androidExtensions.experimental = true

    // Setting reports location for each module and using junit platform
    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
            // Always show the result of every unit test, even if it passes.
            ignoreFailures = true
            // path to the directory in which results will be stored (prefix connected- + modulename)
            reports.junitXml.destination = file("../build/connected-$project.name")
        }

        junitPlatform {
            filters {
                engines {
                    include 'junit', 'junit-jupiter'
                }
            }
            jacocoOptions.taskGenerationEnabled = false
        }
    }

}

allprojects {
    tasks.withType(JavaCompile).all {
        options.compilerArgs += '-Adagger.gradle.incremental=enabled'
        options.compilerArgs += [
                '-Xlint:-deprecation',
                "-Xlint:unchecked"
        ]
    }

    afterEvaluate {
        extensions.findByName('kapt')?.arguments {
            arg("dagger.gradle.incremental", "enabled")
        }
    }
}